!ODVK.zpl

!ODVk Macro v.1.1b edu
!Otkupman D.G. (odvk@ya.ru)
!Важно: старые версии Zemax не поддерживают комментарии с символом «#»
FILE$  = $FILENAME()
TITLE$ = $LENSNAME()

PRINT "© ODVk"
PRINT "Файл   : ", FILE$ #Название файла
PRINT "Система: ", TITLE$ #Наименование системы
PRINT #Пустая строка

INPUT "Размер (шаг) элемента матричного приемника излучения, мкм (OK или Enter для пропуска)", a

PRINT "	Основные параметры"
PRINT
!Определение основной размерности
IF (UNIT() == 0)
      u$ = " мм"
   ELSE (UNIT() == 1)
      u$ = " см"
   ELSE (UNIT() == 2)
      u$ = " ''"
   ELSE
      u$ = " м"
ENDIF
FORMAT 0.5 #Цифра после точки показывает количество отображаемых десятичных знаков для последующих расчётов
   GETSYSTEMDATA 1
PRINT "Фокусное расстояние ", vec1 (7), u$
PRINT "Диафрагменное число ", vec1 (10)
PRINT "Входной зрачок диаметром ", vec1 (11), u$,
PRINT " удалён от первой поверхности на ", vec1 (12), u$
PRINT "Выходной зрачок диаметром ", vec1 (13), u$,
PRINT " удалён от плоскости изображения на ", vec1 (14), u$
FORMAT 1.0
PRINT "Апертурная диафрагма расположена на поверхности ", vec1 (23)
FORMAT 0.6
IF (FTYP() == 0)
   PRINT "Поле системы ", 2*MAXF(), " град."
ELSE
   PRINT "Поле системы ", 2*MAXF(), u$
ENDIF
PRINT "Главная длина волны ", WAVL(PWAV()), " мкм"
IF (WAVL(NWAV())-WAVL(1) == 0)
   PRINT "Монохроматическая система" #Если задана одна длина волны
ELSE
   PRINT "Спектральный диапазон ", WAVL(1), "-", WAVL(NWAV()), " мкм"
ENDIF
FORMAT 0.4
PRINT "Угловое увеличение ", vec1 (17)
PRINT "Относительная освещенность на краю поля ", RELI(NFLD())
IF (vec1 (25) == 1) THEN PRINT "Пространство объектов телецентрическое"
IF (vec1 (30) == 1) THEN PRINT "Пространство изображений афокальное"
IF (vec1 (19) == 1) THEN PRINT "Использовано нацеливание лучей"
IF (vec1 (3) != 0) THEN PRINT "Задана аподизация"
IF (vec1 (4) == 1) THEN PRINT "Температура ", vec1 (5), " град. Цельсия, ", "давление ", vec1 (6), " атм"
PRINT "Полная длина оптической системы ", vec1 (18), u$,
f_L = vec1 (18) / vec1 (7)
PRINT " (", f_L, " фокуса)"
PRINT "Масса оптической системы ", TMAS(), " гр"
PRINT
PRINT "	Конструктивные параметры"
Line50$="——————————————————————————————————————————————————"
Line47$="———————————————————————————————————————————————"
rdn1$=" №         Радиус       Толщина    Показатель Материал"
rdn2$="         кривизны        на оси   преломления         "
dke1$="          Диаметр       Стрелка  Коническая"
dke2$="                        прогиба  постоянная"
PRINT Line50$, Line47$
PRINT rdn1$, dke1$
PRINT rdn2$, dke2$
PRINT Line50$, Line47$
FOR s = 1, NSUR(), 1
   x = 0
   y = SDIA(s)
   z = SAGG(x,y,s)
   IF (s == vec1(23))
      PRINT "АД", #Апертурная диафрагма
   ELSE
      IF (s == NSUR())
         PRINT "ПИ", #Плоскость изображения или приёмник излучения
      ELSE
         FORMAT 2.0
         PRINT s,
      ENDIF
   ENDIF 
   FORMAT 14.5
   Material$ = $GLASS(s)
   Material$ = $LEFTSTRING(Material$, 11)

   PRINT " ", RADI(s), THIC(s), INDX(s), " ", Material$, SDIA(s)*2, z,
   Conic = CONI(s)
   FORMAT 12.5
   IF (Conic != 0)
      PRINT Conic
   ELSE
      PRINT
   ENDIF
NEXT
PRINT Line50$, Line47$
!Функция передачи модуляции
FORMAT 0.3
   IF (a == 0)
PRINT #Обход ошибки ввода
   ELSE
v = 1/(2*a*1E-3) #Частота Найквиста
PRINT "Период (шаг) элемента ", a, " мкм"
PRINT "Пространственная частота ", v, " линий (пар) на мм"
PRINT "Полихроматическая оптическая передаточная функция на частоте матричного приёмника:"
 PRINT "Для минимального значения поля"
   GETMTF v, 0, 0, 3, 1, 1
PRINT "тангенциальное сечение:", vec1 (0)
PRINT "сагиттальное сечение  :", vec1 (1)
 PRINT "Для максимального значения поля"
   GETMTF v, 0, NFLD(), 3, 1, 1
PRINT "тангенциальное сечение:", vec1 (0)
PRINT "сагиттальное сечение  :", vec1 (1)
   ENDIF
BEEP
